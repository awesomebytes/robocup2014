sp {gp*propose*follow
   (state <s> ^name gp
              ^robot <r>
              ^person <p>
              ^desired <d>)
   (<r> ^locId <rloc>)
   (<p> ^followed no
        ^near yes
        ^id <pid>)
   (<d> ^name follow
        ^person <dp>)
   (<dp> ^id <pid>
         ^followed yes)
-->
   (<s> ^operator <op> + =)
   (<op> ^name follow
         ^toPers <p>
         ^withRob <r>)
}

sp {gp*apply*follow
   (state <s> ^operator <op>
              ^io.output-link <out>)
   (<op> ^name follow
         ^toPers <p>)
   (<p> ^id <pid>)
-->
   (<out> ^follow.pers <pid>)
}

sp {gp*apply*follow*remove-follow
   (state <s> ^io.output-link <out>
              ^operator <op>
              ^person <p>
              ^robot <r>)
   (<out> ^follow <st>)
   (<st> ^status complete
         ^pers <pid>)
   (<op> ^name follow
         ^toPers <pop>)
   (<pop> ^id <pid>)
   (<p> ^id <pid>
        ^followed <blaf>
        ^near <blan>)
-->
   (<p> ^followed yes
                  <blaf> -
        ^near yes
              <blan> -)
   (<out> ^follow <st> -)
}